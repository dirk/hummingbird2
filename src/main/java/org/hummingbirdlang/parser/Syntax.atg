COMPILER Syntax

CHARACTERS

letter = 'A'..'Z' + 'a'..'z'.
nonZeroDigit = "123456789".
digit = nonZeroDigit + "0".
tab = '\t'.
lf = '\n'.
cr = '\r'.
stringChar = ANY - "\"" - '\\' - cr - lf.

TOKENS

identifier = letter {letter | digit | "_"}.
let = "let".
var = "var".
lf = lf.
semicolon = ";".
integerLiteral = "0" | nonZeroDigit.
floatFractional = "." {digit}.

COMMENTS FROM "//" TO lf
COMMENTS FROM "/*" TO "*/"

IGNORE cr

PRODUCTIONS

Syntax =
  Source<out Object statements>
                                 (. this.program = statements; .)
  .

Source<out Object result> =
  SourceStatements<out HBStatementNode[] statements> (. result = HBSourceRootNodeFactory.create(source.createUnavailableSection(), statements); .).

SourceStatements<out HBStatementNode[] result> =
                                             (. List<HBStatementNode> body = new ArrayList<>(); .)
  {
    Statement<out HBStatementNode statement> (. body.add(statement); .)
  }
                                             (. result = body.toArray(new HBStatementNode[body.size()]); .)
  .

Statement<out HBStatementNode result> =
                                 (. result = null; .)
  (
      LetDeclaration<out result>
    | VarDeclaration<out result>
    | Expression<out result>
  )
  Terminators // More than one terminator is fine.
  .

Terminators =
  Terminator
  { Terminator }
  .

Terminator =
  lf | semicolon
  .

LetDeclaration<out HBStatementNode result> =
  let
  "="
  Expression<out HBExpressionNode expression> (. result = expression; .)
  .

VarDeclaration<out HBStatementNode result> =
  var
  "="
  Expression<out HBExpressionNode expression> (. result = expression; .)
  .

Expression<out HBExpressionNode result> =
  TernaryExpression<out result>.

TernaryExpression<out HBExpressionNode result> =
                                                        (. result = null; .)
  LogicalOrExpression<out HBExpressionNode condOrValue> (. result = condOrValue; .)
  [
    "?" LogicalOrExpression<out HBExpressionNode then>
    ":" LogicalOrExpression<out HBExpressionNode els>
  ]
  .

LogicalOrExpression<out HBExpressionNode result> =  (. result = null; .)
  LogicalAndExpression<out HBExpressionNode left>   (. result = left; .)
  [
    "||"
    LogicalOrExpression<out HBExpressionNode right> (. result = new HBLogicalOrNode(left, right); .)
  ]
  .

LogicalAndExpression<out HBExpressionNode result> =  (. result = null; .)
  GroupOrTupleExpression<out HBExpressionNode left>  (. result = left; .)
  [
    "&&"
    LogicalAndExpression<out HBExpressionNode right> (. result = new HBLogicalAndNode(left, right); .)
  ]
  .

GroupOrTupleExpression<out HBExpressionNode result> = (. result = null; .)
  (
    (
                                                      (. boolean isTuple = false; .)
      "("
      Expression<out Object expression>
      [
                                                      (. isTuple = true;
                                                         List<Object> elements = new ArrayList<>();
                                                         elements.add(expression); .)
        ","
        [
          Expression<out Object secondElement>        (. elements.add(secondElement); .)
          {
            ","
            Expression<out Object nextElement>        (. elements.add(nextElement); .)
          }
        ]
      ]
      ")"
    )                                                 (. result = null; /* TODO: Tuple expression! */ .)
  |
    SuffixExpression<out HBExpressionNode suffix>     (. result = suffix; .)
  )
  .

SuffixExpression<out HBExpressionNode result> =
  Atom<out HBExpressionNode atom>               (. result = atom; .)
  [
    Suffix<out result, atom>
  ]
  .

Atom<out HBExpressionNode result> =
                                 (. result = null; .)
  (
      IdentifierAtom<out result>
    | NumericAtom<out result>
  )
  .

IdentifierAtom<out HBExpressionNode result> =
  identifier (. result = new HBIdentifierNode(t); .)
  .

NumericAtom<out HBExpressionNode result> =
                          (. result = null; .)
  (
    integerLiteral        (. StringBuilder number = new StringBuilder(t.val); .)
    (
      // No decimal, so it's an int.
                          (. result = new HBIntegerLiteralNode(number.toString()); .)
      |
        "."               (. number.append(t.val); .)
        {
          floatFractional (. number.append(t.val); .)
        }
                          (. result = new HBFloatLiteralNode(number.toString()); .)
    )
  )
  .

Suffix<out HBExpressionNode result, HBExpressionNode parent> =
                                                               (. result = null; .)
  (
      (
          "("                                                  (. HBExpressionNode parameter;
                                                                  List<HBExpressionNode> parameters = new ArrayList<>(); .)
          [
            Expression<out parameter>                          (. parameters.add(parameter); .)
            {
              ","
              Expression<out parameter>                        (. parameters.add(parameter); .)
            }
          ]
          ")"                                                  (. result = HBCallNodeFactory.create(parent, parameters); .)
        |
          "["
          Expression<out HBExpressionNode index>
          "]"                                                  (. result = new HBIndexerNode(parent, index); .)
        |
          "."
          identifier                                           (. result = new HBPropertyNode(parent, t); .)
      )
      [
        Suffix<out result, result>
      ]
    |
      // Assignment must be last so we avoid the recursive branch.
      Assignment<out HBExpressionNode newValue>                (. result = new HBAssignmentNode(parent, newValue); .)
  )
  .

Assignment<out HBExpressionNode newValue> =
  "="
  Expression<out newValue>
  .

END Syntax.
